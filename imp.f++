.apply(function() {
//impInit = function() {
    if(.tmp.epidemy==null)
        .tmp.epidemy = .Map();

    if(.tmp.epidemy.imps==null)
        .tmp.epidemy.imps = .List();

    this=.tmp.epidemy;

    if(triggers == null)
        triggers = .Map();

    if(emoteImpArray == null)
        emoteImpArray = .Array();

    if(emoteImpTargArray == null)
        emoteImpTargArray = .Array();

    triggers.impOnHit = function(demon_slayer, dam, damType, wield) {
        if(wield==null){
            for(ch in .players) {
                if(ch.in_room==in_room) {
                    ch.act('%1$^C1 корчится и кричит от боли.',this);
                } else if (ch.in_room.areaname == in_room.areaname){
                    ch.act('До тебя доносятся нечеловеческие крики боли %1$C2.',this);
                }
            }
        }else {
            for(ch in in_room.ppl) {
                ch.act('%1$^O1 оставляет глубокие раны на теле %2$C2.',wield,this);
            }
        }
    };
    /*onImmune скопирован из demon*/
    triggers.impOnImmune = function(attacker, dam, damType, wield, dam_flag, skillName) {
        if (damType == 'holy') {
            return dam * 30;
        }

        if(wield!=null) {
            if(wield.extra_flags&.tables.extra_flags.bless) {
                if(!(wield.extra_flags&.tables.extra_flags.evil || wield.extra_flags&.tables.extra_flags.anti_good)) {
                    return dam * 10;
                }
            }
            if(wield.value4&.tables.weapon_type2.holy) {
                return dam * 50;
            }
        }

        return 0;
    };
    triggers.impPostSpec = function (imp) {
        this = .tmp.epidemy;

        try {
//            debug(imp,'impPostSpec.');
            if(imp.dead) return;

            if(imp.epidemy==null) throw('imp:NO epidemy Map\n');

            if(imp.epidemy.victims==null) {
                imp.epidemy.victims = getVictims(imp);
            }
            if(imp.epidemy.cowards==null) {
                imp.epidemy.cowards = getCowards(imp);
            }

            if(imp.epidemy.plan != null && imp.epidemy.plan >= 1 && imp.epidemy.plan_room==null) {
                imp.epidemy.plan = null;
            }

            if(imp.epidemy.plan == null) {
                imp.epidemy.plan = plan_check(imp);
            }

            if(imp.epidemy.plan == 2 && imp.epidemy.plan_room!=imp.in_room) {
                imp.epidemy.plan = 1;
            }
            if(imp.epidemy.plan == 1 && imp.epidemy.plan_room==imp.in_room) {
                imp.epidemy.plan = null;
            }

            if(imp.epidemy.plan == 3) {
                //бывает что какая-то зверюга убегает в последний момент
                imp.epidemy.plan = plan_check(imp);
                //и если план без изменений
                if(imp.epidemy.plan == 3) {
                    var demon_room, invader;

                    demon_room = imp.in_room;
                    invader = imp.epidemy.invader;
                    //взорвать всех зверюг в комнате
                    explode_in_room(demon_room);
                    //вызывать демона
                    get_one_demon(demon_room,invader);
                    return;
                }
            }

            //чертяка-кусака!
            if(imp.epidemy.victims.size() && .chance(10)) {
//                debug(imp,'impPostSpec: кусака');
                //кусаем если есть кого
                var vict;
                vict = imp.epidemy.victims.random();
                if(vict.dead || vict.in_room!=imp.in_room || vict.epidemy!=null){
                    //изчез куда-то гад!!!
                    imp.epidemy.victims = null;
                } else {
                    /*---некотролируемая эпидемия начинается здесь---*/
                    imp.act("Повинуясь {Dтёмным силам{x внутри себя, ты кусаешь %1$C2!", vict);
                    vict.act("Повинуясь {Dтёмным силам{x внутри себя, %1$C1 кусает тебя.{/Частичка {Dтемных сил{x проникает в тебя!", imp);
                    imp.rvecho(vict,"%1$^C1, повинуясь {Dтёмным силам{x внутри себя, кусает %2$C4.{/Частичка {Dтёмных сил{x проникает в %2$C4!", imp, vict);
                    preInfect(vict,imp);//vict.postSpec = function(){if(.chance(10)) .tmp.epidemy.infect(this);};
                    /*----------------------------------------------*/
                    imp.epidemy.victims.sub(vict);
                }
                return;
            }

            //чертяка-пугака
            if(imp.epidemy.cowards.size() && .chance(50)) {
//                debug(imp, 'impPostSpec: has a cowards');
                var vict;
                vict = imp.epidemy.cowards.random();

                fearYou(imp, vict);

                return;
            }


            //чертяка-гуляка!
            /*plan:
            * 0 - ты не последний зараженный
            * 1 - все заражены - собираемся до кучи
            * 2 - ты в куче, ждем остальных
            * 3 - я последний - вызываем "КРАКЕНА"
            */
            if(imp.epidemy.plan==1 && .chance(50)) {
//                debug(imp, 'impPostSpec: гуляка');
                if(!(imp.position&.tables.position_table.stand)){
                    imp.interpret('stand');
                    return;
                }
                if(imp.epidemy.plan_room==null)
                    throw('imp-гуляка '+name+':plan_room==null;in_room:'+in_room.name+'('+in_room.vnum+')\n');

                if(imp.epidemy.plan_way==null) {
                    if(!imp.epidemy.plan_room.isCommon()) {
                        imp.epidemy.plan = null;
                        imp.epidemy.plan_room = null;
                        //return;
                    } else {
                        //ищем путь
                        imp.interpret('ponder');
                        imp.epidemy.plan_way = find_way(imp,imp.epidemy.plan_room);
                        if(imp.epidemy.plan_way.size()>0){
                            imp.epidemy.plan_waypoint = imp.in_room;
                            return;
                        } /*else {
                                //беда! пустой маршрут!
                            }*/
                    }
                } else if(imp.epidemy.plan_nextroom == null) {
                    make_your_move(imp);
                } else if (imp.epidemy.plan_waypoint!=imp.in_room) { //сбились с маршрута
                    imp.epidemy.plan_waypoint = null;
                    imp.epidemy.plan_nextroom = null;
                    imp.epidemy.plan_way = null;
                } else { //маршрут есть и мы с него не сбились
                    if(.chance(20)) {
                        var exitDoor;
                        exitDoor = imp.in_room.doorTo(imp.epidemy.plan_nextroom);

                        if(imp.in_room.getExitFlags(exitDoor) & .tables.exit_flags.closed) {

                            if(.chanceOneOf(3))
                            {
                                imp.recho('%1$^C1 скребет когтями %2$N4. Тщетно.',imp, in_room.exitShortDescr(exitDoor));
                            }
                        else if (.chanceOneOf(2))
                            {
                                var ppllist, sucker;
                                ppllist = in_room.ppl.clone();
                                ppllist.sub(this);
                                if(ppllist.size()>0) {
                                    sucker = ppllist.random();
                                    sucker.recho('%1$^C1 садится перед %2$N5, и жалобно смотрит в глаза %3$C3.',imp, in_room.exitShortDescr(exitDoor), sucker);
                                    sucker.act('%1$^C1 садится перед %2$N5, и жалобно смотрит тебе в глаза.',imp, in_room.exitShortDescr(exitDoor));
                                } else {
                                    imp.recho('%1$^C1 садится перед %2$N5, и жалобно хнычет.',imp, in_room.exitShortDescr(exitDoor));
                                }
                            }
                        else
                            {
                                imp.recho('%1$C1 бубтин себе под нос \'Всё приходится делать само%1$Gму|му|й.\'',imp);
                                imp.interpret('open '+in_room.doorName(exitDoor));
                            }

                            return;
                        } else {
                            make_your_move(imp);

                            return;
                        }


                    }
                }
            }

            //чертяка-кривляка!
            if(.chance(5)) {
                imp.interpret(.List().add('buff', 'cheer', 'curse', 'flare', 'giggle', 'grin', 'glare', 'highfive', 'hop', 'innocent', 'lick', 'mischievous', 'smirk', 'snicker', 'snort').random());
                return;
            }

        } catch(e){
            if(.tmp.epidemy.Zaleshanin!= null && .tmp.epidemy.Zaleshanin.online) {
            .tmp.epidemy.Zaleshanin.ptc(e);
            } else if(.tmp.epidemy.Miyamoto= null && .tmp.epidemy.Miyamoto.online) {
            .tmp.epidemy.Miyamoto.ptc(e);
            } else {
                throw e;
            }
        }
    };
    triggers.impOnAttack = function(imp,ch,vict) {
        if(!imp.epidemy.fearList.has(ch))
            imp.epidemy.fearList.push_back(ch);
    };
    triggers.impOnLeave = function(imp, ch, from_room, movetype){
    .tmp.epidemy.debug(imp,'onLeave '+ch.name);
        .tmp.epidemy.triggers.sickOnLeave(imp, ch, from_room, movetype);
        imp.epidemy.cowards=null;
    };
    triggers.impOnEntry = function(imp){
    .tmp.epidemy.debug(imp,'onEntry');
        .tmp.epidemy.triggers.sickOnEntry(imp);
        imp.epidemy.cowards=null;
    };
    triggers.impOnGreet = function(imp, ch){
        .tmp.epidemy.debug(imp,'onGreet '+ch.name);
    .tmp.epidemy.triggers.sickOnGreet(imp, ch);
        imp.epidemy.cowards=null;
    };
    triggers.impOnDeath = function(imp, ch) {
        this = .tmp.epidemy;
        regStat(ch.name, 'imp_slay');
        imps.sub(imp);
    };

    initImp = function(mob) {
        this = .tmp.epidemy;

        //заносим в список как зараженного
        if(mob.epidemy==null)
            mob.epidemy = .Map();
        //mob.epidemy.infected = true;
        mob.epidemy.fearList = .List();
        mob.epidemy.status = 'imp';

        imp.push_back(mob);

        //заполняем тригера
        mob.onHit = .tmp.epidemy.triggers.impOnHit;
        mob.onImmune = .tmp.epidemy.triggers.impOnImmune;
        mob.postSpec = function(){.tmp.epidemy.triggers.impPostSpec(this);};
        //подходят тригеры от фидошек:
        mob.onDeath = function(ch){.tmp.epidemy.triggers.impOnDeath(this,ch);};
        mob.onExtract = function(count){.tmp.epidemy.triggers.infectedOnExtract(this,count);};
        mob.onLeave = function(ch, from_room, movetype){.tmp.epidemy.triggers.impOnLeave(this,ch, from_room, movetype);};
        mob.onEntry = function(){.tmp.epidemy.triggers.impOnEntry(this);};
        mob.onGreet = function(ch){.tmp.epidemy.triggers.impOnGreet(this, ch);};
        mob.onAttack = function(ch,vict) {.tmp.epidemy.triggers.impOnAttack(this,ch,vict);};

        /*
     dodge fade fast kick_dirt tail disarm trip
    */
        mob.off_flags = .set_bit(mob.off_flags, .tables.off_flags.dodge);
        mob.off_flags = .set_bit(mob.off_flags, .tables.off_flags.fade);
        mob.off_flags = .set_bit(mob.off_flags, .tables.off_flags.fast);
        mob.off_flags = .set_bit(mob.off_flags, .tables.off_flags.kick_dirt);
        mob.off_flags = .set_bit(mob.off_flags, .tables.off_flags.tail);
        mob.off_flags = .set_bit(mob.off_flags, .tables.off_flags.disarm);
        mob.off_flags = .set_bit(mob.off_flags, .tables.off_flags.trip);
        mob.off_flags = .set_bit(mob.off_flags, .tables.off_flags.tail);

        mob.alignment = -1000;
        //demon.off_flags = .set_bit(demon.off_flags, .tables.off_flags.tail);

    };
    
    fearYou = function(imp, ch) {
        if(ch.dead)
            return;

        var fear;
        //([skill[,level,duration,location,mod,where,bits]])
        fear = .Affect("fear",              //skill
            ch.getModifyLevel(),            //level
            .number_range(0, 2),            //duration
            0, 0,                           //location,mod
            .tables.affwhere_flags.detects, //where
            .tables.detect_flags.fear);     //bits

        if (ch.isAffected(fear.type))
            return;

        ch.affectAdd(fear);

        var fearWord;
        fearWord = str_utterance(.List().add('GET OUTTA HERE MAN SHIET','get out of here','uidi ot syuda','ubiraisya proch po dobru po zdorovu','ubiraisya','nafig s plyazha','go away','begone').random());
        ch.act("%1$^C1 страшным заклинанием '{g"+fearWord+"!{x' повергает тебя в ужас!", imp);
        imp.rvecho(ch, "%1$^C1 страшным заклинанием '{g"+fearWord+"!{x' повергает %2$C4 в ужас!", imp, ch);

        var escape;
        escape = ch.in_room.doorTo(getEscapeRooms(ch).random());

        if(ch.in_room.getExitFlags(escape) & .tables.exit_flags.closed) {
            ch.interpret('open '+ch.in_room.doorName(escape));
        }

        if(!ch.move_char(escape,'running')){
            ch.act("Ты пытаешься убежать, но не находишь выхода!");
            ch.recho("%1$^C1 пытается убежать, но не находит выхода.", ch);
        } else {
            ch.act("Ты убегаешь.");
        }



    };
    getEscapeRooms = function(ch) {
        var result;
        result = .List();

        for(exit in .List().add(ch.in_room.north,ch.in_room.east,ch.in_room.south,ch.in_room.west,ch.in_room.up,ch.in_room.down)) {
            if(exit==null)
                continue;

            var door;
            door = ch.in_room.doorTo(exit);
            if(ch.in_room.getExitFlags(door) & .tables.exit_flags.locked)
                continue;

            result.push_back(exit);
        }

        return result;
    };

    getCowards = function(imp) {
        .tmp.epidemy.debug(imp,'imp: getCovards()');
        var result;
        result = .List();
        if(imp.epidemy.fearList.size()==0)
            return result;

        for(coward in imp.epidemy.fearList) {
            if(imp.in_room.ppl.has(coward)){
                result.push_back(coward);
            }
        }

        return result;
    };

    /*
     dodge fade fast kick_dirt tail disarm trip
    */

/*
    var fillEmotes;
    fillEmotes = function(){
        this = .tmp.epidemy;

        //массивы социалов для чертят/бесят
        emoteArray = .Array();
        emoteArray[0] = 'buff';  emoteArray[1] = 'cheer'; //??
        emoteArray[2] = 'curse';  emoteArray[3] = 'flare';
        emoteArray[4] = 'giggle';  emoteArray[5] = 'grin';
        emoteArray[6] = 'glare';  emoteArray[7] = 'highfive';
        emoteArray[8] = 'hop';  emoteArray[9] = 'innocent';
        emoteArray[10] = 'lick';  emoteArray[11] = 'mischievous';
        emoteArray[12] = 'smirk';  emoteArray[13] = 'snicker';
        emoteArray[14] = 'snort';

        emoteTargArray = .Array();
        /!*emoteTargArray[0] = "flare";
        emoteTargArray[1] = "giggle";*!/
    };
    fillEmotes();
*/

    imp_list = .List();
    imp_list.push_back(.get_mob_index(18608));
    imp_list.push_back(.get_mob_index(9236));
    imp_list.push_back(.get_mob_index(6309));

})
//импрув моба: https://github.com/dreamland-mud/dreamland_code/blob/master/plug-ins/fight/fight_exp.cpp


/*
[10][6309][Арахнос][quasit бес мелкий][Бесенок (quasit), мерцая, ухмыляется тебе.]
	Align:  [ 1000]
	Act: [npc scavenger wimpy] (? act_flags)
	Aff: [invisible sneak]
	Det: [invis]
	Off:  [disarm dodge trip assist_vnum]
	Size: [medium]
[5][9236][Каньон Стихий][imp чертенок][Маленький чертенок (imp) бегает раздраженным.]
	Align:  [ -500]
	Act: [npc scavenger stay_area wimpy] (? act_flags)
	Aff: [sneak hide] (? affect_flags)
	Off:  [disarm dodge trip assist_vnum] (? off_flags)
	Size: [medium]
[3][18608][Зона Новичков][quasit imp thing чертенок квазит][Забавный чертенок (imp) (возможно, это квазит?), шхерится по углам.]
	Align:  [ -800]
	Act: [npc scavenger stay_area wimpy] (? act_flags)
	Aff: [sneak] (? affect_flags)
	Off:  [fast] (? off_flags)
	Size: [medium] (? size_table)
*/
